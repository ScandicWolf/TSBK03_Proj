#version 430
layout(local_size_x = 256) in;

layout(std430, binding = 0) readonly buffer Htbuf { vec2 Ht[]; };
layout(std430, binding = 1) writeonly buffer Dxbuf { vec2 Dx[]; };
layout(std430, binding = 2) writeonly buffer Dzbuf { vec2 Dz[]; };

uniform int u_N;

const float PI = 3.14159265358979323846;

void main() {
    uint id = gl_GlobalInvocationID.x;
    int N = u_N;
    if (id >= uint(N*N)) return;

    int x = int(id % N);
    int y = int(id / N);

    float kx = float(x - N/2) * (2.0 * PI / float(N));
    float ky = float(y - N/2) * (2.0 * PI / float(N));

    vec2 H = Ht[id];

    float k_len = length(vec2(kx, ky));
    if (k_len < 1e-6) {
        Dx[id] = vec2(0.0);
        Dz[id] = vec2(0.0);
        return;
    }

    vec2 Dxk = vec2(
        (kx/k_len) * H.y,
       -(kx/k_len) * H.x
    );

    vec2 Dzk = vec2(
        (ky/k_len) * H.y,
       -(ky/k_len) * H.x
    );

    Dx[id] = Dxk;
    Dz[id] = Dzk;
}
